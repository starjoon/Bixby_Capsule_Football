result-view {
  match {
    MyPreference (result) {
      from-output: CheckPreference
    }
  }

  message {
    if ($can.device != 'bixby-mobile') {
      template ("즐겨찾기 확인") {
        speech ("즐겨찾기 선수는 모바일에서 확인해주세요.")
      }
    }
    else {
      if (size(result) != 0) {
        template ("즐겨찾기 선수는 다음과 같습니다.")
      }
      else {
        template ("즐겨찾기에 등록된 선수가 없습니다.")
      }
    }
  }

  render {
    layout {
      section {
        content {
          for-each (result) {
            as (player) {
              thumbnail-card {
                on-click {
                  intent {
                    goal: GoToPlayer
                    value: $expr (player.name)
                  }
                }
                title-area {
                  halign (Center)
                  slot1 {
                    text {
                      style (Title_S)
                      value {
                        template ("#{value(player.name)}")
                      }
                    }
                  }
                  slot2 {
                    single-line {
                      text {
                        style (Detail_M_Soft)
                        value {
                          template ("2019-20 시즌")
                        }
                      }
                    }
                  }
                  slot3 {
                    single-line {
                      text {
                        style (Title_L)
                        value {
                          template ("#{value(player.matchesPlayed)}")
                        }
                      }
                      spacer
                      text {
                        style (Detail_M)
                        value {
                          template ("경기")
                        }
                      }
                      spacer
                      text {
                        style (Title_L)
                        value {
                          template ("#{value(player.goal)}")
                        }
                      }
                      spacer
                      text {
                        style (Detail_M)
                        value {
                          template ("득점")
                        }
                      }
                      spacer
                      text {
                        style (Title_L)
                        value {
                          template ("#{value(player.assist)}")
                        }
                      }
                      spacer
                      text {
                        style (Detail_M)
                        value {
                          template ("도움")
                        }
                      }
                    }
                  }
                }
                image-url {
                  template ("#{value(player.profPic)}")
                }
                image-position (Start)
                image-object-fit (Contain)
              }
            }
          }
        }
      }
    }
  }
  conversation-drivers {
    if (size(result) != 0) {
      conversation-driver {
        template-macro (PREF)
      }
    }
    else {
      conversation-driver {
        template-macro (PREF-NEW)
      }
    }
  }
}